apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'realm-android'
//apply plugin: 'io.fabric'

android {
  compileSdkVersion 26
  buildToolsVersion '26.0.0'

  dexOptions {
    maxProcessCount 4
    preDexLibraries false
    javaMaxHeapSize "8g"
  }

  androidExtensions {
    experimental = true
  }

  defaultConfig {
    defaultPublishConfig 'debug'

    applicationId "com.mutlak.metis.wordmem"
    minSdkVersion 16
    targetSdkVersion 26
    testInstrumentationRunner "${applicationId}.runner.RxAndroidJUnitRunner"
    versionCode 1000
    // Major -> Millions, Minor -> Thousands, Bugfix -> Hundreds. E.g 1.3.72 == 1,003,072
    versionName '0.1.0'

    buildConfigField("String", "POKEAPI_API_URL",
        "\"${PokeapiApiUrl}\"")

    multiDexEnabled true

    vectorDrawables.useSupportLibrary = true
  }
  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }

  sourceSets {
    def commonTestDir = 'src/commonTest/java'
    test {
      java.srcDir commonTestDir
    }
    androidTest {
      java.srcDir commonTestDir
    }
  }

  lintOptions {
    textOutput "stdout"
    textReport true
    checkAllWarnings true
    warningsAsErrors true
    showAll true
    explainIssues true
    abortOnError false
    lintConfig file("$projectDir/lint.xml")
  }

  packagingOptions {
    exclude 'META-INF/services/javax.annotation.processing.Processor'
    exclude 'LICENSE.txt'
    exclude 'META-INF/license/LICENSE.base64.txt'
    exclude 'META-INF/NOTICE.txt'
    exclude 'META-INF/LICENSE.txt'
    exclude 'META-INF/rxjava.properties'
    exclude 'META-INF/MANIFEST.MF'
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  testOptions {
    unitTests {
      returnDefaultValues = true
    }
  }
}

configurations.all {
  resolutionStrategy {
    force 'com.squareup.okio:okio:1.11.0'
    force "com.squareup.okhttp3:okhttp:3.5.0"
    force 'org.jetbrains.kotlin:kotlin-stdlib:1.1.2-3'
  }
}

apply from: 'dependencies.gradle'

dependencies {

  implementation supportLibs
  implementation networkLibs
  implementation rxJavaLibs
  implementation googlePlayLibs

  implementation 'com.android.support:multidex:1.0.1'
  implementation 'com.github.bumptech.glide:glide:3.7.0'
  implementation('com.github.bumptech.glide:okhttp3-integration:1.4.0') {
    exclude group: 'glide-parent'
  }

  implementation 'uk.co.chrisjenx:calligraphy:2.3.0'
  implementation 'com.victor:lib:1.0.4'
  implementation 'com.github.amlcurran.showcaseview:library:5.4.3'
  implementation 'com.github.florent37:tutoshowcase:1.0.0'
  implementation 'com.github.paolorotolo:appintro:4.1.0'
  implementation 'com.github.zagum:Android-SwitchIcon:1.3.2'
  implementation 'com.annimon:stream:1.1.8'
  implementation 'com.github.pwittchen:prefser:2.0.6'
  implementation 'com.github.florent37:arclayout:1.0.2'

  //RxPermission for easy runtime permission management
  implementation 'com.tbruyelle.rxpermissions:rxpermissions:0.9.1@aar'

  implementation "com.afollestad.material-dialogs:core:$versions.materialDialog"
  implementation "com.afollestad.material-dialogs:commons:$versions.materialDialog"


  implementation "com.jakewharton:butterknife:$versions.butterKnife"
  implementation "com.jakewharton.timber:timber:4.5.1"
  implementation "com.facebook.stetho:stetho-okhttp3:1.5.0"

  implementation "com.google.dagger:dagger:$versions.dagger"
  implementation 'com.android.support.constraint:constraint-layout:1.0.2'
  compileOnly 'org.glassfish:javax.annotation:10.0-b28' //Required by Dagger2

  debugImplementation leakCannary
  releaseImplementation leakCannary
  testImplementation leakCannary

  // APT dependencies
  kapt "com.jakewharton:butterknife-compiler:$versions.butterKnife"
  kapt daggerCompiler
  kaptTest daggerCompiler
  kaptAndroidTest daggerCompiler

  testImplementation unitTestLibs
  androidTestImplementation androidTestsLibs

  implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

  testImplementation "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
  testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
}

// Log out test results to console
tasks.matching { it instanceof Test }.all {
  testLogging.events = ["failed", "passed", "skipped"]
}
